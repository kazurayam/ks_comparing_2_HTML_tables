plugins {
  id 'java'
}

version = '0.2.3'

ext {
    AUTO_IMPORTED_JAR_PREFIX = 'AUTOIMPORTED_'
}

repositories {
  mavenCentral()
  mavenLocal()
}

configurations {
    myconf
}

dependencies {

  // https://mvnrepository.com/artifact/io.github.java-diff-utils/java-diff-utils
  myconf group: 'io.github.java-diff-utils', name: 'java-diff-utils', version: '4.11'

  // https://mvnrepository.com/artifact/com.kazurayam/materialstore
  myconf group: 'com.kazurayam', name: 'materialstore', version: '0.4.1'

  myconf group: 'org.freemarker', name: 'freemarker', version: "2.3.31"

  // https://mvnrepository.com/artifact/com.kazurayam/junit4ks
  myconf group: 'com.kazurayam', name: 'junit4ks', version: '1.6.1'

}

task drivers {
    doFirst {
        delete fileTree("Drivers") {
            include("**/" + AUTO_IMPORTED_JAR_PREFIX + "*")
        }
    }
    doLast {
        copy { copySpec ->
            copySpec
                .from(project.getConfigurations().getByName("myconf"))
                .into("Drivers")
                .include(
                    "**/junit4ks*.jar",
                    "**/java-diff-utils*.jar",
                    "**/materialstore*.jar",
                    "**/freemarker*.jar"
                    )
                .rename({ s ->
                    AUTO_IMPORTED_JAR_PREFIX + s
                    })
        }
    }
}
